if !def(Q_SPRITE_INC_3dMG2Z3W)
def Q_SPRITE_INC_3dMG2Z3W equ 1


def bSprite_ESC equ 7
def Sprite_END equ $80

;; Sprob coordinates are unsigned values in range [0..64) with the origin point being at the Render origin - 32
def SprobCoordMax equ 64
def SprobOrigin equ SprobCoordMax / 2

def SprobW equ 8
def SprobH equ 16

;; Y position to centre a sprob
def SprobCentreY equ SprobOrigin + SprobH / 2
;; X position to centre a sprob
def SprobCentreX equ SprobOrigin + SprobW / 2


;; sprob Y, X, I, A
macro sprob
	assert fail, _NARG == 4
	db (\1), (\2), (\3), (\4)
endm


;; sprob_opts_reset [opt]
macro sprob_opts_reset
	redef __SPROB_OPTS equ 1
	redef _sprCY equ 0
	redef _sprCX equ 0
	redef _sprSY equ 0
	redef _sprSX equ 0
	redef _sprSI equ 0
	redef _sprFlipX equ 0

	sprob_opts \#
endm


;; sprob_opts [FLAG], [KEY:VALUE]
macro sprob_opts
	if !def(__SPROB_OPTS)
		sprob_opts_reset
	endc
	rept _NARG
		redef _optSepIdx equ strfind("\<1>", ":")
		if _optSepIdx != -1
			redef _optKey equs strslice("\<1>", 0, _optSepIdx)
			redef _optVal equs strslice("\<1>", _optSepIdx+1)
			if strcmp("{_optKey}", "sy") == 0
				redef _sprSY equ ({_optVal})
			elif strcmp("{_optKey}", "sx") == 0
				redef _sprSX equ ({_optVal})
			elif strcmp("{_optKey}", "si") == 0
				redef _sprSI equ ({_optVal})
			endc
			purge _optKey, _optVal
		elif strcmp("\<1>", "flipx") == 0
			redef _sprFlipX equ 1
		elif strcmp("\<1>", "cx") == 0
			redef _sprCX equ SprobCentreX
		elif strcmp("\<1>", "cy") == 0
			redef _sprCY equ SprobCentreY
		else
			fail "Unknown sprob opt {d:I}: '{"\<I>"}'"
		endc
		shift
		purge _optSepIdx
	endr
endm


;; sprob_seq N, Y, X, I, A, [opt]
macro sprob_seq
	assert fail, _NARG >= 5
	def _sprN equ (\1)
	assert fail, _sprN >= 0
	def _sprY equ (\2)
	def _sprX equ (\3)
	def _sprI equ (\4)
	def _sprA equ (\5)
	shift 5

	sprob_opts \#

	rept _sprN
		sprob _sprCY + _sprY, _sprCX + _sprX, _sprI, _sprA
		redef _sprY equ _sprY + _sprSY
		redef _sprX equ _sprX + _sprSX
		redef _sprI equ _sprI + _sprSI
	endr

	purge _sprN, _sprY, _sprX, _sprI, _sprA
endm


macro sprob_end
	assert fail, _NARG == 0
	db Sprite_END
endm


endc ; Q_SPRITE_INC_3dMG2Z3W

