include "sprite.rgbinc"


def bFlipX equ 5
def bFlipY equ 6


section "hSprx", hram
hSprxOriginY:: db
hSprxOriginX:: db


section "Sprx", rom0
;; @param HL: Sprx to render
;; @param B,C: Y,X render position
;; @mut: AF, BC, DE, HL
SprxRender::
	ld a, b :: sub SprobOrigin :: ldh [hSprxOriginY], a
	ld a, c :: sub SprobOrigin :: ldh [hSprxOriginX], a

:
	; peek the first sprob byte
	bit bSprite_ESC, [hl]
	jr nz, .proc_esc
	ldh a, [hSprxOriginY] :: ld b, a
	ldh a, [hSprxOriginX] :: ld c, a
	call SprobRender
	jr :-

	ret
.proc_esc:
	; There's only one escape code: END -- terminate the sprite descriptor.
	ret


;; @param HL: Sprx to render
;; @param B,C: Y,X render position
;; @param D: I mod
;; @param E: A mod (ADD the palette, XOR the rest)
;; @mut: AF, HL
SprxRenderMod::
.loop
	; peek the first sprob byte
	bit bSprite_ESC, [hl]
	jr nz, .proc_esc
	push bc
	push de

	; Y position, flip
	ld a, [hl+] :: sub SprobCentreY
	bit bFlipY, e
	jr z, :+
	cpl :: inc a
:
	add b :: ld b, a

	; X position, flip
	ld a, [hl+] :: sub SprobCentreX
	bit bFlipX, e
	jr z, :+
	cpl :: inc a
:
	add c :: ld c, a

	ld a, [hl+] :: add d :: ld d, a
	; Palette mod gets added, rest of bits get xored.
	; Yes, this is confusioning.
	; xor atrbmod, keep palmod unchanged
	ld a, [hl]
	and $F8
	xor e
	ld e, a
	; add palmod
	ld a, [hl+]
	add e
	; combine adjusted palette with adjusted atrbs
	xor e
	and $07
	xor e
	ld e, a

	push hl
	call ObdoSingle
	pop hl
	pop de
	pop bc
	jr .loop

	ret
.proc_esc:
	ret


;; Push one Sprob to OAM
;; @param HL: pointer to Sprob
;; @param B,C: Y,X Sprobspace origin
;; @ret HL: pointer at end of Sprob
;; @mut: AF, BC, DE, HL
SprobRender:
	ld a, [hl+] :: add b :: ld b, a
	ld a, [hl+] :: add c :: ld c, a
	ld a, [hl+] :: ld d, a
	ld a, [hl+] :: ld e, a
	push hl
	call ObdoSingle
	pop hl
	ret


/*
section "SprxTestSprxs", rom0
sprNil:
	db Sprite_END
spr1:
	; <0, 0> should be RenderPos - <32, 32>
	db 0, 0, 0, 1
	db 0, 63, 2, 2
	db 63, 63, 4, 3
	db 63, 0, 6, 4
	db Sprite_END
spr2:
	db 24 + 16, 24 + 8, 16, 0
	db 24 + 16, 32 + 8, 18, 0
	db Sprite_END
spr3:
	db 24 + 16, 24 + 8, 16, 1
	db 24 + 16, 32 + 8, 18, 2
	db Sprite_END
*/

