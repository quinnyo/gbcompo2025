include "entity.rgbinc"
include "collide.rgbinc"
include "coord.rgbinc"


section "hSim", hram
hSim::
	.gravity:: db
	.tick: db
	.global_accel: db


section "wEntity", wram0, align[8]
wEntity:: ds ENTITY_COUNT_MAX * szEntity
_pad: ds 256 - (@ - wEntity)


section "Entity Testo", rom0
EntityTesto::
	ld hl, wEntity
	ld c, ENTITY_COUNT_MAX
:
	ld a, ENTINFOF_ALIVE | $01
	ld [hl+], a
	ld a, ENTITY_COUNT_MAX
	sub c
	ld [hl+], a
	; accx
	ld a, -10 :: ld [hl+], a
	; velx
	ld a, -100 :: ld [hl+], a
	; posx
	coord_ld_hli_int (WORLD_ORIGIN >> 4) + 90
	; accy
	ld a, -10 :: ld [hl+], a
	; vely
	ld a, -100 :: ld [hl+], a
	; posy
	coord_ld_hli_int (WORLD_ORIGIN >> 4) + 50

	ld a, l
	and $F0
	add szEntity
	ld l, a

	dec c
	jr nz, :-
	ret


section "Entity", rom0
EntityInit::
	ld hl, wEntity
	ld b, ENTITY_COUNT_MAX
.entity_pool_loop
	ld c, szEntity
:
	ld [hl+], a
	dec c
	jr nz, :-
	dec b
	jr nz, .entity_pool_loop

	call EntityTesto

	ld a, GRAVITY_DEFAULT
	ldh [hSim.gravity], a
	xor a
	ldh [hSim.tick], a
	ldh [hSim.global_accel], a
	ret


; @mut: AF, BC, DE, HL
EntityUpdate::
;	ldh a, [hSim.global_accel] :: ld b, a
	ld b, 0
	; Should apply gravity?
	ldh a, [hSim.tick]
	bit 0, a
	jr z, :+
	ldh a, [hSim.gravity]
	ld b, a
:
	ldh a, [hSim.global_accel]
	add b
	ldh [hSim.global_accel], a

	ld hl, wEntity
.loop
	bit ENTINFOB_ALIVE, [hl] :: jr z, .next

	; Update motion X, zero constant accel
	entity_index Entity_AccX
	xor a :: call _motion_go
	ld a, b :: ldh [hCollide.vx], a
	ld a, [hl+] :: ldh [hCollide.px+0], a
	ld a, [hl+] :: ldh [hCollide.px+1], a

	; Flap!
	entity_index Entity_Ctrl
	call _flap_update :: ld b, a

	entity_index Entity_AccY
	ldh a, [hSim.global_accel]
	add b :: call _motion_go
	ld a, b :: ldh [hCollide.vy], a
	ld a, [hl+] :: ldh [hCollide.py+0], a
	ld a, [hl+] :: ldh [hCollide.py+1], a

	push hl
	call CollidePoint
	pop hl

	entity_index Entity_PosX
	ldh a, [hCollide.px+0] :: ld [hl+], a
	ldh a, [hCollide.px+1] :: ld [hl+], a
	entity_index Entity_PosY
	ldh a, [hCollide.py+0] :: ld [hl+], a
	ldh a, [hCollide.py+1] :: ld [hl+], a

.next
assert sizeof("wEntity") == 256
	entity_index szEntity
	jp nc, .loop

	ldh a, [hSim.tick]
	inc a
	ldh [hSim.tick], a
	xor a
	ldh [hSim.global_accel], a
	ret


;; Update flap effect and return current flap impulse (Y axis accel)
;; @param HL: pointer to entity ctrl field
;; @ret A: flap impulse
;; @mut: AF
_flap_update:
	ld a, mENTCTRL_FLAP
	and [hl]
	ret z
	; count down
	dec [hl]
	; calculate flap impulse/accel
	cpl :: inc a
	ret


; @param HL: Pointer to beginning of an Entity motion block.
; @param A: constant accel
; @ret B: velocity for the current frame
; @mut: AF, B, HL
_motion_go:
	; Add accel accumulator to the constant accel param. Hold on to that.
	add [hl] :: ld b, a
	; Clear the accumulator
	xor a :: ld [hl+], a
	; Apply accel
	; TODO: handle potential overflow (vel + accel)
	ld a, [hl]
	add b
	; Apply friction
	ld b, a
	call _fric_1_4
	add b

	; Grab the "old" velocity value before writing the updated one.
	ld b, [hl]
	; Write the the new velocity (with friction applied)
	ld [hl+], a
	ret


; Calculate simple friction (1/16) for the given velocity.
; The result is an acceleration (with magnitude in the range `[1..abs(vel)]`) opposed to the direction (sign) of the input velocity, or zero if the velocity is zero.
; @param A: vel
; @ret A: friction(vel)
; @mut: AF
_fric_1_16:
	and a :: ret z
	cp $80
	jr nc, .negative
	; positive: divide first
	swap a :: and $0E
	cpl; :: inc a
	;dec a
	ret
.negative
	; negative: invert first
	cpl :: inc a
	swap a :: and $0E
	inc a
	ret

_fric_1_32:
	and a :: ret z
	cp $80
	jr nc, .negative
	; positive: divide first
	rlca :: rlca :: rlca :: and $06
	cpl; :: inc a
	;dec a
	ret
.negative
	; negative: invert first
	cpl :: inc a
	rlca :: rlca :: rlca :: and $06
	inc a
	ret

_fric_1_8:
	and a :: ret z
	cp $80
	jr nc, .negative
	; positive: divide first
	rrca :: rrca :: rrca :: and $1E
	cpl; :: inc a
	;dec a
	ret
.negative
	; negative: invert first
	cpl :: inc a
	rrca :: rrca :: rrca :: and $1E
	inc a
	ret

_fric_1_4:
	and a :: ret z
	cp $80
	jr nc, .negative
	; positive: divide first
	rrca :: rrca :: and $3E
	cpl; :: inc a
	;dec a
	ret
.negative
	; negative: invert first
	cpl :: inc a
	rrca :: rrca :: and $3E
	inc a
	ret

_fric_1_2:
	and a :: ret z
	cp $80
	jr nc, .negative
	; positive: divide first
	rrca :: and $7E
	cpl; :: inc a
	;dec a
	ret
.negative
	; negative: invert first
	cpl :: inc a
	rrca :: and $7E
	inc a
	ret

