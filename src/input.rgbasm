include "hardware.inc"


section "Input State", hram
;; Current input state -- keys that are depressed.
hKeys:: db
;; Keys that were pressed in the most recent update.
hKeysPressed:: db
;; Keys that were released in the most recent update.
hKeysReleased:: db


section "Input", rom0
InputInit::
	xor a
	ldh [hKeys], a
	ldh [hKeysPressed], a
	ldh [hKeysReleased], a
	ret


; Process the new input state.
; @param B: new input state
; @mut: A, C
InputUpdate::
	ldh a, [hKeys]
	xor b
	ld c, a ; C = keys that changed
	and b ; A = keys that changed to pressed
	ldh [hKeysPressed], a
	ld a, b ; A = new state
	ldh [hKeys], a
	cpl
	and c
	ldh [hKeysReleased], a
	ret


; Read current input state
; @ret B: input state
; @mut: A
InputRead::
	ld a, P1F_GET_DPAD
	ldh [rP1], a
	ldh a, [rP1]
	ldh a, [rP1]
	ld b, a
	ld a, P1F_GET_BTN
	ldh [rP1], a
	ld a, b
	or $F0
	swap a
	ld b, a
	ldh a, [rP1]
	or $F0
	xor b
	ld b, a
	ld a, P1F_GET_NONE
	ldh [rP1], a
	ret

